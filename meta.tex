% ----------------------------------------------------------------------
\begin{frame}{What is this?}
  \bigskip
  \begin{itemize}
  \item A technique in which programs treat other programs as data
    \bigskip
  \item<2-> Workflow
    \begin{enumerate}
    \item Translate a logic program into facts
    \item Combine the obtained facts with a meta logic program
    \end{enumerate}
    \medskip
  \item<3-> The first step is called \alert{reification}
  \end{itemize}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile]{An example}
  \begin{itemize}
  \item \structure{Logic program} \texttt{ezy.lp}
\begin{lstlisting}
{a}.
b :- a.
c :- not a.
\end{lstlisting}
  \item<2-> \structure{Implicit statements}
\begin{lstlisting}
#show a.
#show b.
#show c.
\end{lstlisting}
  \item<3-> \structure{Reification}
\begin{lstlisting}
$ clingo --output=reify ezy.lp
\end{lstlisting}
  \end{itemize}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile,shrink=28]{An example, reified}
\bigskip
\begin{lstlisting}
$ clingo --output=reify ezy.lp
atom_tuple(0).
atom_tuple(0,1).
literal_tuple(0).
rule(choice(0),normal(0)).
atom_tuple(1).
atom_tuple(1,2).
literal_tuple(1).
literal_tuple(1,-1).
rule(disjunction(1),normal(1)).
atom_tuple(2).
atom_tuple(2,3).
literal_tuple(2).
literal_tuple(2,1).
rule(disjunction(2),normal(2)).
output(a,2).
literal_tuple(3).
literal_tuple(3,3).
output(b,3).
literal_tuple(4).
literal_tuple(4,2).
output(c,4).
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile]{An example, again}
\bigskip
\begin{lstlisting}
  {a}.
  b :- a.
  c :- not a.

  #show a.
  #show b.
  #show c.
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile,shrink=28]{An example, reified again}
\bigskip
\begin{lstlisting}
rule(choice(0),normal(0)).      atom_tuple(0).   literal_tuple(0).
                                atom_tuple(0,1).

rule(disjunction(1),normal(1)). atom_tuple(1).   literal_tuple(1).
                                atom_tuple(1,2). literal_tuple(1,-1).

rule(disjunction(2),normal(2)). atom_tuple(2).   literal_tuple(2).
                                atom_tuple(2,3). literal_tuple(2,1).

output(a,2).

output(b,3).                                     literal_tuple(3).
                                                 literal_tuple(3,3).

output(c,4).                                     literal_tuple(4).
                                                 literal_tuple(4,2).
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile,shrink=34]{Meta encoding, or ASP in ASP}
\bigskip
\begin{lstlisting}
conjunction(B) :- literal_tuple(B),
        hold(L) : literal_tuple(B, L), L > 0;
    not hold(L) : literal_tuple(B,-L), L > 0.

body(normal(B)) :- rule(_,normal(B)), conjunction(B).
body(sum(B,G))  :- rule(_,sum(B,G)),
    #sum { W,L :     hold(L), weighted_literal_tuple(B, L,W), L > 0 ;
           W,L : not hold(L), weighted_literal_tuple(B,-L,W), L > 0 } >= G.

  hold(A) : atom_tuple(H,A)   :- rule(disjunction(H),B), body(B).
{ hold(A) : atom_tuple(H,A) } :- rule(     choice(H),B), body(B).

#show.
#show T : output(T,B), conjunction(B).
\end{lstlisting}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile]{An example, running}
  \begin{itemize}
  \item \structure{Logic program} \texttt{ezy.lp}
\begin{lstlisting}
{a}.
b :- a.
c :- not a.
\end{lstlisting}
  \item<2-> \structure{Running}
\begin{lstlisting}[basicstyle=\small\ttfamily]
$ clingo ezy.lp 0
clingo version 5.5.0
Reading from ezy.lp
Solving...
Answer: 1
c
Answer: 2
a b
SATISFIABLE
\end{lstlisting}
  \end{itemize}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}[fragile]{An example, running reified}
  \begin{itemize}
  \item \structure{Logic program} \texttt{ezy.lp}
\begin{lstlisting}
{a}.
b :- a.
c :- not a.
\end{lstlisting}
  \item<2-> \structure{Running reified}
\begin{lstlisting}[basicstyle=\small\ttfamily]
$ clingo --output=reify ezy.lp | clingo - meta.lp 0
clingo version 5.5.0
Reading from - ...
Solving...
Answer: 1
c
Answer: 2
a b
SATISFIABLE
\end{lstlisting}
  \end{itemize}
\end{frame}
% ----------------------------------------------------------------------
\begin{frame}{Some applications}
  \begin{itemize}
  \item Re-interpretation of language constructs
  \item Interpretation of new language constructs
    \medskip
  \item Computation of classical models
  \item Computation of supported models
  \item Computation of here-and-there models
  \item Computation of diverse stable models
  \item Computation of preferred models
    \medskip
  \item Checking program equivalence
    \medskip
  \item Guess-and-check programming
  \end{itemize}
\end{frame}
% ----------------------------------------------------------------------
%
%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../../main"
%%% End:
